group 'com.adeptik'
version '1.0.1'

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {

}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    group = 'documentation'

    from javadoc
}
task sourceJar(type: Jar) {
    group = 'build'

    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }

            pom.withXml {
                asNode().with {
                    appendNode('name', 'Adeptik Command Line Utils')
                    appendNode('description', 'Utilities for creating command line applications')
                    appendNode('url', 'https://github.com/SnrdTeam/command-line-utils-java')
                    appendNode('licenses').appendNode('license').with {
                        appendNode('name', 'Apache 2.0')
                        appendNode('url', 'https://github.com/SnrdTeam/command-line-utils-java/blob/master/LICENSE')
                    }
                }
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}